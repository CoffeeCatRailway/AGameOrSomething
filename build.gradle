plugins {
    id "java"
}

compileJava*.options*.encoding = 'UTF-8'

group = project.projGroup
version = project.projVersion

repositories {
    mavenCentral()
    maven { url "https://libraries.minecraft.net" }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation(platform("org.lwjgl:lwjgl-bom:${project.lwjglVersion}"))
    implementation group: "org.joml", name: "joml", version: project.jomlVersion

    implementation "org.lwjgl:lwjgl"

    def platforms = ['linux', 'macos', 'macos-arm64', 'windows']
    for (def lwjglModule : ['glfw', 'opengl', 'openal', 'stb']) {
        implementation "org.lwjgl:lwjgl-$lwjglModule"

        for (def platform : platforms) {
            implementation "org.lwjgl:lwjgl-$lwjglModule::natives-$platform"
        }
    }

    for (def platform : platforms) {
        implementation "org.lwjgl:lwjgl::natives-$platform"
    }

    compileOnly 'org.jetbrains:annotations:23.0.0'
    implementation "com.mojang:logging:1.0.0"
    implementation 'org.slf4j:slf4j-api:2.0.3'

    runtimeOnly "net.minecrell:terminalconsoleappender:1.3.0"
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j2-impl:${project.log4jVersion}"
    runtimeOnly "org.apache.logging.log4j:log4j-api:${project.log4jVersion}"
    runtimeOnly "org.apache.logging.log4j:log4j-core:${project.log4jVersion}"

    implementation "io.netty:netty-all:${project.nettyVersion}"
    implementation "org.bouncycastle:bcpkix-jdk15on:1.65"
    implementation "com.github.Ocelot5836:glfw-windows:96e3de7"

    implementation "org.agrona:agrona:${project.agronaVersion}"

    implementation "com.google.code.gson:gson:${project.gsonVersion}"
}

jar {
    manifest.attributes["Main-Class"] = "io.github.coffeecatrailway.agameorsomething.core.AGameOrSomething"
    manifest.attributes["Implementation-Title"] = "A Game or Somthing"
    manifest.attributes["Implementation-Version"] = project.projVersion

    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}